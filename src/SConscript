"""Build ping test"""
import os

HEADERS = ['iof_plugin.h', 'ios_gah.h']
COMMON_SRC = ['version.c', 'v2.c', 'ios_gah.c', 'log.c']

def scons():
    """Scons function"""

    Import('env prereqs')

    # Set options which are used throughout the src.
    tenv = env.Clone()
    tenv.Append(CFLAGS='-pthread')
    tenv.Append(CPPDEFINES=['_FILE_OFFSET_BITS=64'])
    tenv.Append(CPPPATH=['common/include'])

    prereqs.require(tenv, 'mercury', 'mcl')

    # Build a static library of the common parts.
    common = []
    for src in COMMON_SRC:
        common += tenv.Object(os.path.join('common', src))

    # Now build the CNSS application
    cenv = tenv.Clone()

    # Set the define in SConscript so that it's in place for the prereqs.require
    # check which will verify fuse3/fuse.h can be loaded.
    cenv.Append(CPPFLAGS=['-DFUSE_USE_VERSION=30'])

    if cenv.get('fuse3'):
        prereqs.require(cenv, 'fuse')
        cenv.Append(CPPDEFINES=['IOF_USE_FUSE3=1'])
    else:
        if env.get('PLATFORM') == 'darwin':
            cenv.Append(CPPPATH=['/usr/local/include/osxfuse'],
                        LIBS=['osxfuse'])
        else:
            cenv.Append(LIBS=['fuse'])

    progs = cenv.Program(['cnss/cnss.c', common])

    # Build the IONSS application
    ienv = tenv.Clone()
    ienv.Append(LIBS='pthread')
    progs += ienv.Program(['ionss/ionss.c', common])

    # Build tester
    progs += tenv.Program(['test/test_gah.c', common])

    tenv.Install(os.path.join("$PREFIX", 'bin'), progs)

if __name__ == "SCons.Script":
    scons()
