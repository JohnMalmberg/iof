"""Build ping test"""
import os
Import('ENV PREREQS')

# Set options which are used throughout the src.
TENV = ENV.Clone()
TENV.Append(CFLAGS='-pthread')
TENV.Append(CPPDEFINES=['_FILE_OFFSET_BITS=64'])
TENV.Append(CPPPATH=['common/include'])

# Build a static library of the common parts.
COMMON = TENV.Object('common/version')
COMMON += TENV.Object('common/v2')

# Now build the CNSS application
CENV = TENV.Clone()

# Set the define in SConscript so that it's in place for the PREREQS.require
# check which will verify fuse3/fuse.h can be loaded.
CENV.Append(CPPFLAGS=['-DFUSE_USE_VERSION=30'])

USE_FUSE3 = CENV.get('fuse3')

if USE_FUSE3:
    PREREQS.require(CENV, 'fuse')
    CENV.Append(CPPDEFINES=['IOF_USE_FUSE3=1'])
else:
    if ENV.get('PLATFORM') == 'darwin':
        CENV.Append(CPPPATH=['/usr/local/include/osxfuse'],
                    LIBS=['osxfuse'])
    else:
        CENV.Append(LIBS=['fuse'])

PROGS = CENV.Program(['cnss/cnss.c', COMMON])

# Build the IONSS application
IENV = TENV.Clone()
IENV.Append(LIBS='pthread')
PROGS += IENV.Program(['ionss/ionss.c', COMMON])

TENV.Install(os.path.join("$PREFIX", 'bin'), PROGS)
