"""Build ping test"""
import os
Import('ENV PREREQS')

TENV = ENV.Clone()
TENV.Prepend(CPPPATH=['../common/include'])
TENV.Append(LIBS=['pthread'])
TENV.Append(CFLAGS=['-lpthread'])
PREREQS.require(TENV, "pmix", "mercury")

# Build the process set shared library, which requires the common
# library.
LIBPS = TENV.SharedLibrary(target='pset', source=['process_set.c'],
                           LIBS=['$LIBS', 'proto_common'],
                           LIBPATH=['$LIBPATH', '#build/iof/proto/common'])

COMMON = TENV.Object('test_ps_common')
# Build the test program.
PS = TENV.Program(['test_ps.c', COMMON],
                  LIBS=['$LIBS', 'pset', 'proto_common'],
                  LIBPATH=['$LIBPATH', '#build/iof/proto/common',
                           '#build/iof/proto/process_set'])

TENV.Install(os.path.join("$PREFIX", 'lib'), LIBPS)
TENV.Install(os.path.join("$PREFIX", 'bin'), PS)
TENV.Install(os.path.join("$PREFIX", 'include'), 'include/process_set.h')

TENV.Append(RPATH=os.path.join("$PREFIX", 'lib'))
