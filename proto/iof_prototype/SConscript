"""Build ping test"""
import os
Import('ENV PREREQS')

TENV = ENV.Clone()
TENV.Append(CPPDEFINES=['FUSE_USE_VERSION=30'])
PREREQS.require(TENV, "pmix", "mercury")

USE_FUSE3 = TENV.get('fuse3')

if USE_FUSE3:
    PREREQS.require(TENV, 'fuse')
    TENV.Append(CPPDEFINES=['IOF_USE_FUSE3=1'])
else:
    if ENV.get('PLATFORM') == 'darwin':
        TENV.Append(CPPPATH=['/usr/local/include/osxfuse'],
                    LIBS=['osxfuse'])
    else:
        TENV.Append(LIBS=['fuse'])

TENV.Append(CFLAGS=['-D_FILE_OFFSET_BITS=64'])
TENV.Append(CPPPATH=['#proto/process_set/include'])
COMMON = TENV.Object('rpc_common')
COMMON += TENV.Object('rpc_handler')
COMMON += TENV.Object('server_backend')

if ENV.get('PLATFORM') == 'posix':
    TENV.Append(LIBS=['pthread'])

TENV.Append(LIBS=['pset'], LIBPATH=['#build/iof/proto/process_set'])
PROGS = TENV.Program(['client_main.c', COMMON],
                     LIBS=['$LIBS', 'proto_common'],
                     LIBPATH=['$LIBPATH', '../common'])
PROGS += TENV.Program(['server_main.c', COMMON],
                      LIBS=['$LIBS', 'proto_common'],
                      LIBPATH=['$LIBPATH', '../common'])

TENV.Install(os.path.join("$PREFIX", 'bin'), PROGS)
TENV.Append(RPATH=os.path.join("$PREFIX", 'lib'))
