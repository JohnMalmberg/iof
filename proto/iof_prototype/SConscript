"""Build ping test"""
Import('ENV PREREQS')
import os

TENV = ENV.Clone()
PREREQS.require(TENV, "pmix", "mercury", "bmi")
TENV.Append(CFLAGS=['-D_FILE_OFFSET_BITS=64'])
TENV.Prepend(CPPPATH=['../common/include'])
COMMON = TENV.Object('rpc_common')
COMMON += TENV.Object('rpc_handler')
COMMON += TENV.Object('server_backend')

if ENV['PLATFORM'] == 'darwin':
    TENV.Append(CFLAGS=['-I/usr/local/include/osxfuse'],
                LIBS=['osxfuse'])
else:
    TENV.Append(LIBS=['fuse', 'pthread'])

PROGS = TENV.Program(['client_main.c', COMMON],
                     LIBS=['$LIBS', 'proto_common'],
                     LIBPATH=['$LIBPATH', '../common'])
PROGS += TENV.Program(['server_main.c', COMMON],
                      LIBS=['$LIBS', 'proto_common'],
                      LIBPATH=['$LIBPATH', '../common'])

TENV.Install(os.path.join("$PREFIX", 'bin'), PROGS)
TENV.Append(RPATH=os.path.join("$PREFIX", 'lib'))
